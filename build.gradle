plugins {
	id 'babric-loom' version '0.12-SNAPSHOT'
//	id 'io.github.juuxel.loom-quiltflower' version '1.7.2'
	id 'maven-publish'
}

// Fetch build number from Github Actions
def ENV = System.getenv()
def build_number = ENV.BUILD_NUMBER ?: "local"
version = "b1.7.3+build." + build_number

archivesBaseName = project.archives_base_name
group = project.maven_group

loom {
	gluedMinecraftJar()
	customMinecraftManifest.set("https://babric.github.io/manifest-polyfill/${minecraft_version}.json")
	accessWidenerPath = file("src/main/resources/forge.accesswidener")
}

repositories {
	mavenCentral()
	maven {
		name 'Babric'
		url 'https://maven.glass-launcher.net/babric'
		content {
			includeGroup 'babric'
			includeGroup 'org.lwjgl.lwjgl'
		}
	}
	maven {
		name 'halotroop'
		url 'https://repo.blucobalt.dev/repository/maven-halotroop'
		content {
			includeGroup 'com.halotroop'
			includeGroup 'io.github.betterthanupdates'
		}
	}
	mavenLocal()

	maven {
		name 'Modrinth'
		url 'https://api.modrinth.com/maven'
		content {
			includeGroup 'maven.modrinth'
		}
	}
	maven {
		name 'JitPack'
		url 'https://jitpack.io'
		content {
			includeGroup 'com.github.*'
		}
	}

	flatDir {
		dirs "./remapped/client"
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "io.github.betterthanupdates:yarn:b1.7.3+build.${project.mappings}"
	modApi "babric:fabric-loader:${project.loader_version}"

	implementation "org.slf4j:slf4j-api:1.8.0-beta4"
	implementation "org.apache.logging.log4j:log4j-slf4j18-impl:2.17.2"

	include modApi("maven.modrinth:mod-remapping-api:1.1.0")

	// Done
//	modRuntimeOnly(group: "remapped.client", name: "modloader", version: "b1.7.3")
//	modRuntimeOnly(group: "remapped.client", name: "modloadermp-1.7.3-unofficial", version: "v2")
	// Todo
//	modRuntimeOnly(group: "remapped.client", name: "minecraftforge-client", version: "1.0.7-20110907")
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

java {
	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8

	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}" }
	}
	from("FORGE_LICENSE")
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	repositories {
		if (ENV.MAVEN_URL) {
			maven {
				name 'halotroop-blucobalt.dev'
				url 'https://repo.blucobalt.dev/repository/maven-halotroop'
				credentials {
					username ENV.MAVEN_USERNAME
					password ENV.MAVEN_PASSWORD
				}
			}
		} else {
			mavenLocal()
		}
	}
}
